USE DBTRANSPORTE
GO

/* ===== ROLES Y PERMISOS ===== */
CREATE TABLE ROL(
    IdRol INT PRIMARY KEY IDENTITY,
    Descripcion VARCHAR(50),
    FechaRegistro DATETIME DEFAULT GETDATE()
);

CREATE TABLE PERMISO(
    IdPermiso INT PRIMARY KEY IDENTITY,
    IdRol INT REFERENCES ROL(IdRol),
    NombreMenu VARCHAR(100),
    FechaRegistro DATETIME DEFAULT GETDATE()
);

CREATE TABLE USUARIO(
    IdUsuario INT PRIMARY KEY IDENTITY,
    Documento VARCHAR(50),
    NombreCompleto VARCHAR(100),
    Correo VARCHAR(100),
    Clave VARCHAR(100),
    IdRol INT REFERENCES ROL(IdRol),
    Estado BIT,
    FechaRegistro DATETIME DEFAULT GETDATE()
);

/* ===== CLIENTES ===== */
CREATE TABLE CLIENTE(
    IdCliente INT PRIMARY KEY IDENTITY,
    NombreCliente VARCHAR(100) NOT NULL,
    Direccion VARCHAR(200) NOT NULL,
    Estado BIT DEFAULT 1,
    FechaRegistro DATETIME DEFAULT GETDATE()
);

/* ===== MOTORISTAS ===== */
CREATE TABLE MOTORISTA(
    IdMotorista INT PRIMARY KEY IDENTITY,
    NombreMotorista VARCHAR(100) NOT NULL,
    Direccion VARCHAR(200) NOT NULL,
    Correo VARCHAR(100),
    Telefono VARCHAR(20),
    PerfilSocial VARCHAR(200),
    Curriculum VARBINARY(MAX) NULL,
    Estado BIT DEFAULT 1,
    FechaRegistro DATETIME DEFAULT GETDATE()
);

/* ===== UNIDADES DE TRANSPORTE ===== */
CREATE TABLE UNIDAD_TRANSPORTE(
    IdUnidad INT PRIMARY KEY IDENTITY,
    Placa VARCHAR(20) NOT NULL,
    Marca VARCHAR(50),
    CapacidadToneladas DECIMAL(10,2),
    AnioFabricacion INT,
    Estado BIT DEFAULT 1,
    FechaRegistro DATETIME DEFAULT GETDATE()
);

/* ===== ESTADOS DE FLETE ===== */
CREATE TABLE ESTADO_FLETE(
    IdEstado INT PRIMARY KEY IDENTITY,
    Descripcion VARCHAR(50) NOT NULL
);
INSERT INTO ESTADO_FLETE(Descripcion) VALUES
('Programado'),('En Proceso'),('Terminado'),('Con Devolución'),('Con Queja');

/* ===== FLETES ===== */
CREATE TABLE FLETE(
    IdFlete INT PRIMARY KEY IDENTITY,
    IdCliente INT REFERENCES CLIENTE(IdCliente),
    IdUnidad INT REFERENCES UNIDAD_TRANSPORTE(IdUnidad),
    IdMotorista INT REFERENCES MOTORISTA(IdMotorista),
    IdEstado INT REFERENCES ESTADO_FLETE(IdEstado),
    MontoCobro DECIMAL(10,2) NOT NULL,
    LugarRecolecta VARCHAR(200) NOT NULL,
    LugarEntrega VARCHAR(200) NOT NULL,
    HoraSalida DATETIME NOT NULL,
    HoraDestino DATETIME NOT NULL,
    Consideraciones VARCHAR(500),
    FechaRegistro DATETIME DEFAULT GETDATE()
);

/* ===== GASTOS POR FLETE ===== */
CREATE TABLE GASTO_FLETE(
    IdGasto INT PRIMARY KEY IDENTITY,
    IdFlete INT REFERENCES FLETE(IdFlete),
    TipoGasto VARCHAR(50), -- Camión, Varios, Producción
    Concepto VARCHAR(100),
    Monto DECIMAL(10,2),
    FechaRegistro DATETIME DEFAULT GETDATE()
);


********** PROCEDURE PARA CALCULAR UTILIDAD DE FLETE **********

CREATE OR ALTER PROCEDURE sp_CalcularUtilidadFlete
AS
BEGIN
    SET NOCOUNT ON;

    SELECT 
        f.IdFlete,
        c.NombreCliente,
        u.Placa,
        m.NombreMotorista,
        f.MontoCobro AS ValorFlete,
        ISNULL(SUM(g.Monto), 0) AS TotalGastos,
        (f.MontoCobro - ISNULL(SUM(g.Monto), 0)) AS UtilidadNeta,
        CASE 
            WHEN f.MontoCobro > 0 
            THEN ((f.MontoCobro - ISNULL(SUM(g.Monto), 0)) / f.MontoCobro) * 100
            ELSE 0
        END AS PorcentajeUtilidad,
        f.FechaRegistro
    FROM FLETE f
    INNER JOIN CLIENTE c ON f.IdCliente = c.IdCliente
    INNER JOIN UNIDAD_TRANSPORTE u ON f.IdUnidad = u.IdUnidad
    INNER JOIN MOTORISTA m ON f.IdMotorista = m.IdMotorista
    INNER JOIN ESTADO_FLETE e ON f.IdEstado = e.IdEstado
    LEFT JOIN GASTO_FLETE g ON f.IdFlete = g.IdFlete
    WHERE e.Descripcion = 'Terminado'
    GROUP BY f.IdFlete, c.NombreCliente, u.Placa, m.NombreMotorista, f.MontoCobro, f.FechaRegistro
    ORDER BY f.FechaRegistro DESC;
END;
GO


/* ===== STORED PROCEDURE PARA REGISTRAR UNIDAD DE TRANSPORTE ===== */
CREATE PROCEDURE sp_RegistrarUnidadTransporte
(
    @Placa VARCHAR(20),
    @Marca VARCHAR(50),
    @CapacidadToneladas DECIMAL(10,2),
    @AnioFabricacion INT,
    @Estado BIT,
    @Resultado INT OUTPUT,
    @Mensaje VARCHAR(500) OUTPUT
)
AS
BEGIN
    SET @Resultado = 0
    SET @Mensaje = ''

    -- Validar que no exista la placa
    IF EXISTS (SELECT * FROM UNIDAD_TRANSPORTE WHERE Placa = @Placa)
    BEGIN
        SET @Mensaje = 'Ya existe una unidad con esa placa'
        RETURN
    END

    BEGIN TRY
        INSERT INTO UNIDAD_TRANSPORTE (Placa, Marca, CapacidadToneladas, AnioFabricacion, Estado)
        VALUES (@Placa, @Marca, @CapacidadToneladas, @AnioFabricacion, @Estado)

        SET @Resultado = SCOPE_IDENTITY()
        SET @Mensaje = 'Unidad registrada correctamente'
    END TRY
    BEGIN CATCH
        SET @Resultado = 0
        SET @Mensaje = ERROR_MESSAGE()
    END CATCH
END
GO

/* ===== STORED PROCEDURE PARA MODIFICAR UNIDAD DE TRANSPORTE ===== */
CREATE PROCEDURE sp_ModificarUnidadTransporte
(
    @IdUnidad INT,
    @Placa VARCHAR(20),
    @Marca VARCHAR(50),
    @CapacidadToneladas DECIMAL(10,2),
    @AnioFabricacion INT,
    @Estado BIT,
    @Resultado BIT OUTPUT,
    @Mensaje VARCHAR(500) OUTPUT
)
AS
BEGIN
    SET @Resultado = 0
    SET @Mensaje = ''

    -- Validar que no exista otra unidad con la misma placa
    IF EXISTS (SELECT * FROM UNIDAD_TRANSPORTE WHERE Placa = @Placa AND IdUnidad != @IdUnidad)
    BEGIN
        SET @Mensaje = 'Ya existe otra unidad con esa placa'
        RETURN
    END

    BEGIN TRY
        UPDATE UNIDAD_TRANSPORTE 
        SET 
            Placa = @Placa,
            Marca = @Marca,
            CapacidadToneladas = @CapacidadToneladas,
            AnioFabricacion = @AnioFabricacion,
            Estado = @Estado
        WHERE IdUnidad = @IdUnidad

        SET @Resultado = 1
        SET @Mensaje = 'Unidad modificada correctamente'
    END TRY
    BEGIN CATCH
        SET @Resultado = 0
        SET @Mensaje = ERROR_MESSAGE()
    END CATCH
END
GO